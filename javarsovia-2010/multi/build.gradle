def codeProjects() {
	subprojects.findAll { project -> project.name != 'ui' }
}

configure(codeProjects()) {
  apply plugin: 'java'
  group = 'org.gradle.sample'
  version = '1.0'
  repositories {
    mavenCentral()
  }
}


project (':core') {
  dependencies {
    compile 'org.hibernate:hibernate:3.1.3', 'commons-lang:commons-lang:2.5'
    testCompile 'junit:junit:4.8.1'
  }
}


project (':ui') {
  subprojects {
    dependencies {
     compile project (':core')
    }
  }
}

project (':ui:web') {
  apply plugin: 'war'
  apply plugin: 'jetty'

  dependencies {
    compile 'org.apache.wicket:wicket:1.4.8'
    runtime 'org.slf4j:slf4j-simple:1.5.8'
  }
}


project (':ui:swing') {
  apply plugin:  'groovy'
  dependencies {
    groovy "org.codehaus.groovy:groovy-all:1.7.3"
  }

  task run (dependsOn: build) << {
    ant.java(classname: 'org.gradle.sample.ui.swing.SwingApp', fork: true,
         classpath: "${sourceSets.main.runtimeClasspath.asPath}")
    }
}
